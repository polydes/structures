plugins {
    id 'antlr'
	id 'eclipse'
	id 'java-library'
}

java {
	toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

group = 'com.polydes'
version = rootProject.file('version.txt').text.trim()

sourceSets {
	main {
	    antlr {
	        srcDirs = ['src-antlr']
	    }
        java {
            srcDir 'src'
        }
    }
}

processResources {
    from('res') {
    	into 'res/com/polydes/datastruct'
    }
    from('icon.png') {
        into 'com/polydes/datastruct/res'
    }
}

repositories {
    mavenCentral()
    maven {
        allowInsecureProtocol true
        url "http://www.stencyl.com/dl/maven2/releases"
    }
    maven {
        allowInsecureProtocol true
        url "http://www.stencyl.com/dl/maven2/snapshots"
    }
}

configurations {
    api {
        // Undo extendsFrom relationship between the 'antlr' configuration and the 'api' configuration
        // https://github.com/gradle/gradle/issues/820#issuecomment-748623864
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

def antlrVersion = '4.5'
dependencies {
    antlr group: 'org.antlr', name: 'antlr4', version: antlrVersion
    
    //Stencyl and its dependencies
    compileOnly group: 'com.stencyl',        name: 'stencyl',        version: '4.1.0-SNAPSHOT'
    compileOnly group: 'com.stencyl',        name: 'jide-oss',       version: '3.7.11.1'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3',  version: '3.3.2'
	compileOnly group: 'log4j',              name: 'log4j',          version: '1.2.17'
    compileOnly group: 'commons-io',         name: 'commons-io',     version: '2.4'
    
    implementation (group: 'org.antlr',   name: 'antlr4-runtime', version: antlrVersion) {
        exclude group: 'org.abego.treelayout', module: 'org.abego.treelayout.core'
    }
    implementation group: 'tablelayout', name: 'TableLayout',    version: '20050920'
}

jar {
	from {
		configurations.runtimeClasspath.collect { zipTree(it) }
	}
    manifest {
		attributes(
			"Extension-ID": "com.polydes.datastruct",
			"Extension-Main-Class": "com.polydes.datastruct.DataStructuresExtension",
			"Extension-Version": version,
			"Extension-Icon": "com/polydes/datastruct/res/icon.png",
			"Extension-Dependencies": "engine-com.polydes.datastruct-2.0.0,stencyl-4.1.0-b10700",
			"Extension-Type": "game",
			"Extension-Name": "Data Structures Extension",
			"Extension-Description": "Create and Manage Data Structures.",
			"Extension-Author": "Justin Espedal",
			"Extension-Website": "https://github.com/polydes",
			"Extension-Repository": "http://www.polydes.com/repo",
			"Extension-Internal-Version": "4"
		)
	}
}

tasks.register('installEngineToWorkspace', Sync) {
	from('engine')
	into stencylWorkspace+"/engine-extensions/com.polydes.datastruct"
}

tasks.register('installToolsetToWorkspace', Copy) {
    from(tasks.named("jar"))
	into stencylWorkspace+"/extensions"
	rename { fileName -> "com.polydes.datastruct.jar" }
}

tasks.register('installToWorkspace') {
	dependsOn 'installToolsetToWorkspace'
    dependsOn 'installEngineToWorkspace'
}